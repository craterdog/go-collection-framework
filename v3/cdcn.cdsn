
!>
    CRATER DOG COLLECTION NOTATION
    This document is a formal definition of Crater Dog Collection Notation™ (CDCN)
    using Crater Dog Syntax Notation™ (CDSN).
<!


!>
    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the stream of tokens that
    are processed by the parser.  Each token name begins with an upper case letter.  Unlike with
    rule definitions, a token definition cannot specify the name of a rule but can specify the
    name of another token.  Token definitions cannot be recursive and the scanning of tokens is
    not greedy.  Whitespace within a token definition is not ignored.

    The following intrinsic token types are environment or language specific:
     * ANY - any language specific character
     * LOWER_CASE - any language specific lower case character
     * UPPER_CASE - any language specific upper case character
     * DIGIT - any language specific digit
     * ESCAPE - any environment specific escape sequence
     * CONTROL - any environment specific (non-printable) control character
     * EOF - the environment specific end-of-file marker

    Inversion within a token definition may only be applied to a token predicate resulting in a
    single character.  Token inversions cannot be nested.  The following token types are scanned for
    in the order listed below.
<!

$BASE10: '0'..'9'

$BASE16: '0'..'9' | 'a'..'f'

$BOOLEAN: "false" | "true"

$COMPLEX: "(" FLOAT SIGN FLOAT "i)"

$CONTEXT: "array" | "catalog" | "list" | "map" | "queue" | "set" | "stack"

$DELIMITER: "]" | "[" | ")" | "(" | ":" | ","

$EOL: "\n"

$EXPONENT: ('e' | 'E') SIGN ORDINAL

$FLOAT: SIGN? SCALAR EXPONENT?

$FRACTION: '.' BASE10+

$IGNORED: ' '

$INTEGER: ZERO | SIGN? ORDINAL

$LETTER: LOWER_CASE | UPPER_CASE

$NIL: "nil"

$ORDINAL: '1'..'9' '0'..'9'*

$RUNE: "'" ~CONTROL "'"  ! Any printable character.

$SCALAR: (ZERO | ORDINAL) FRACTION

$SIGN: '+' | '-'

$STRING: '"' (ESCAPE | ~('"' | CONTROL))* '"'

$UNICODE: 'u' BASE16{4} | 'U' BASE16{8}

$UNSIGNED: "0x" BASE16+

$ZERO: '0'


!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens generated by the
    scanner.  Each rule name begins with a lower case letter.  The rule definitions may specify the
    names of tokens or other rules and are matched by the parser in the order listed.  A rule
    definition may also be directly or indirectly recursive.  The sequence of factors within
    in a rule definition may be separated by whitespace which is ignored by the parser.

    A predicate within a factor may also be constrained by any of the following cardinalities:
     * predicate{M} - Exactly M instances of the specified predicate.
     * predicate{M..} - M or more instances of the specified predicate.
     * predicate{M..N} - M to N instances of the specified predicate.
     * predicate? - Zero or one instances of the specified predicate (equivalent to predicate{0..1}).
     * predicate* - Zero or more instances of the specified predicate (equivalent to predicate{0..}).
     * predicate+ - One or more instances of the specified predicate (equivalent to predicate{1..}).

    Inversion within a rule definition may only be applied to a token predicate resulting in a
    single token.  Rule inversions cannot be nested.  The following rules are attempted in the order
    listed below.
<!

$association: key ":" value

$associations:
      association ("," association)*
    | EOL (association EOL)+
    | ":"  ! No associations.

$collection: "[" (values | associations) "]" "(" CONTEXT ")"

$key: primitive

$primitive: BOOLEAN | COMPLEX | FLOAT | INTEGER | NIL | RUNE | STRING

$source: collection EOF  ! EOF is the end-of-file marker.

$value: primitive | collection

$values:
      value ("," value)*
    | EOL (value EOL)+
    | " "  ! No values.

