!>
................................................................................
.    Copyright (c) 2009-2024 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
CRATER DOG COLLECTION NOTATION
This document contains a formal definition of the Crater Dog Collection
Notation™ (CDCN) using Crater Dog Syntax Notation™ (CDSN).

A language grammar consists of a set of rule definitions and token definitions.

The following intrinsic character types are context specific:
 * ANY - Any language specific character.
 * LOWER - Any language specific lowercase character.
 * UPPER - Any language specific uppercase character.
 * DIGIT - Any language specific digit.
 * ESCAPE - Any environment specific escape sequence.
 * CONTROL - Any environment specific (non-printable) control character.
 * EOL - The environment specific end-of-line character.
 * EOF - The environment specific end-of-file marker (pseudo character).

A predicate may be constrained by any of the following cardinalities:
 * predicate{M} - Exactly M instances of the specified predicate.
 * predicate{M..} - M or more instances of the specified predicate.
 * predicate{M..N} - M to N instances of the specified predicate.
 * predicate? - Zero or one instances of the specified predicate.
 * predicate* - Zero or more instances of the specified predicate.
 * predicate+ - One or more instances of the specified predicate.

An inversion "~" within a definition may only be applied to an intrinsic
character type or a glyph range.
<!

!>
RULE DEFINITIONS
The following rules are used by the parser when parsing the stream of tokens
generated by the scanner.  Each rule name begins with an uppercase letter.
The rule definitions may specify the names of tokens or other rules and are
matched by the parser in the order listed.  A rule definition may also be
directly or indirectly recursive.  The sequence of factors within in a rule
definition may be separated by spaces which are ignored by the parser.
<!
Source: Collection EOL* EOF  ! Terminated with an end-of-file marker.

Collection: "[" (Associations | Values) "]" "(" context ")"

Associations:
    Association ("," Association)*
    (EOL Association)+ EOL
    ":"  ! No associations.

Association: Key ":" Value

Key: Primitive

Value: Collection | Primitive

Primitive:
    boolean
    complex
    float
    hexadecimal
    integer
    nil
    rune
    string

Values:
    Value ("," Value)*
    (EOL Value)+ EOL
    " "  ! No values.

!>
TOKEN DEFINITIONS
The following token definitions are used by the scanner to generate the
stream of tokens that are processed by the parser.  Each token name begins
with a lowercase letter.  Unlike with rule definitions, a token definition
cannot specify the name of a rule within its definition but it can specify
the name of other tokens.  Token definitions cannot be recursive and the
scanning of tokens is NOT greedy.  And any spaces within a token definition
are NOT ignored.
<!
base10: '0'..'9'

base16: '0'..'9' | 'a'..'f'

boolean: "false" | "true"

complex: "(" float sign float "i)"

context:
    "array"
    "Array"
    "Catalog"
    "List"
    "map"
    "Map"
    "Queue"
    "Set"
    "Stack"

delimiter: "[" | "]" | "(" | ")" | ":" | ","

exponent: ('e' | 'E') sign ordinal

float: sign? scalar exponent?

fraction: '.' base10+

hexadecimal: "0x" base16+

integer: zero | sign? ordinal

letter: LOWER | UPPER

nil: "nil"

ordinal: '1'..'9' '0'..'9'*

rune: "'" ~[CONTROL] "'"  ! Any single printable character.

scalar: (zero | ordinal) fraction

sign: '+' | '-'

string: '"' (ESCAPE | ~['"' CONTROL])* '"'

unicode: 'x' base16{2} | 'u' base16{4} | 'U' base16{8}

zero: '0'

