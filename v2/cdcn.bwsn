!>
    A formal definition of a document containing a collection using Bali Wirth
    Syntax Notationâ„¢ (BWSN):
        <https://github.com/bali-nebula/specifications/blob/main/bwsn.bwsn>

    The token names are identified by all CAPITAL characters and the rule names
    are identified by lowerCamelCase characters. The token and rule definitions
    have been alphabetized to make it easier to locate specific definitions.
    The starting rule is "$document".
<!

$BASE10: "0".."9"

$BASE16: "0".."9" | "a".."f"

$BOOLEAN: "false" | "true"

$COMPLEX: "(" FLOAT SIGN FLOAT "i)"

$CONTEXT: "array" | "catalog" | "list" | "map" | "queue" | "set" | "stack"

$DELIMITER: "]" | "[" | ")" | "(" | ":" | ","

$EOL: "\n"

$ESCAPE: '\' (UNICODE | 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' | '"' | "'" | '\')

$EXPONENT: ("e" | "E") SIGN ORDINAL

$FLOAT: [SIGN] SCALAR [EXPONENT]

$FRACTION: "." <BASE10>

$INTEGER: ZERO | [SIGN] ORDINAL

$NIL: "nil"

$ORDINAL: "1".."9" {"0".."9"}

$RUNE: "'" (ESCAPE | ~("'" | EOL)) "'"

$SCALAR: (ZERO | ORDINAL) FRACTION

$SIGN: "+" | "-"

$STRING: '"' {ESCAPE | ~('"' | EOL)} '"'

$UNICODE: 
    "u" BASE16 BASE16 BASE16 BASE16 |
    "U" BASE16 BASE16 BASE16 BASE16 BASE16 BASE16 BASE16 BASE16

$UNSIGNED: "0x" <BASE16>

$ZERO: "0"

$association: key ":" value

$associations: 
    association {"," association} |
    EOL <association EOL> |
    ":"  ! No associations.

$collection: "[" (values | associations) "]" "(" CONTEXT ")"

$document: collection EOF  ! EOF is the end-of-file marker.

$key: primitive

$primitive: BOOLEAN | COMPLEX | FLOAT | INTEGER | NIL | RUNE | STRING

$value: primitive | collection

$values: 
    value {"," value} |
    EOL <value EOL> |
    ! No values.

