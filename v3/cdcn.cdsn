!>
    CRATER DOG COLLECTION NOTATION
    This document is a formal definition of Crater Dog Collection Notation™
    (CDCN) using Crater Dog Syntax Notation™ (CDSN).
<!

!>
    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the
    stream of tokens that are processed by the parser.  Each token name begins
    with an upper case letter.  Unlike with rule definitions, a token definition
    cannot specify the name of a rule within its definition but it can specify
	the name of other tokens.

    The following intrinsic token types are environment or language specific:
     * ANY - Any language specific character.
     * LOWER - Any language specific lower case character.
     * UPPER - Any language specific upper case character.
     * DIGIT - Any language specific digit.
     * ESCAPE - Any environment specific escape sequence.
     * CONTROL - Any environment specific (non-printable) control character.
     * EOL - The environment specific end-of-line character.
     * EOF - The environment specific end-of-file marker.

    Token definitions cannot be recursive and the scanning of tokens is not
    greedy.  Any spaces within a token definition are NOT ignored.
<!

$BASE10: '0'..'9'

$BASE16: '0'..'9' | 'a'..'f'

$BOOLEAN: "false" | "true"

$COMPLEX: "(" FLOAT SIGN FLOAT "i)"

$DELIMITER: "[" | "]" | "(" | ")" | ":" | ","

$EXPONENT: ('e' | 'E') SIGN ORDINAL

$FLOAT: SIGN? SCALAR EXPONENT?

$FRACTION: '.' BASE10+

$IGNORED: ' '

$INTEGER: ZERO | SIGN? ORDINAL

$LETTER: LOWER | UPPER

$NIL: "nil"

$ORDINAL: '1'..'9' '0'..'9'*

$RUNE: "'" ~CONTROL "'"  ! Any printable character.

$SCALAR: (ZERO | ORDINAL) FRACTION

$SIGN: '+' | '-'

$STRING: '"' (ESCAPE | ~('"' | CONTROL))* '"'

$TYPE: "array" | "map" | "Array" | "Catalog" | "List" | "Map" | "Queue" | "Set" | "Stack"

$UNICODE: 'x' BASE16{2} | 'u' BASE16{4} | 'U' BASE16{8}

$UNSIGNED: "0x" BASE16+

$ZERO: '0'

!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens
    generated by the scanner.  Each rule name begins with a lower case letter.
    The rule definitions may specify the names of tokens or other rules and are
    matched by the parser in the order listed.  A rule definition may also be
    directly or indirectly recursive.  The sequence of factors within in a rule
    definition may be separated by spaces which are ignored by the parser.

    A predicate within a factor may also be constrained by any of the following
    cardinalities:
     * predicate{M} - Exactly M instances of the specified predicate.
     * predicate{M..} - M or more instances of the specified predicate.
     * predicate{M..N} - M to N instances of the specified predicate.
     * predicate? - Zero or one instances of the specified predicate.
     * predicate* - Zero or more instances of the specified predicate.
     * predicate+ - One or more instances of the specified predicate.

    Inversion within a rule definition may only be applied to a token predicate
    resulting in a single token.  Rule inversions cannot be nested.
<!

$document: collection EOF  ! EOF is the end-of-file marker.

$collection: "[" (associations | values) "]" context

$associations:
      association ("," association)*
    | EOL (association EOL)+
    | ":"  ! No associations.

$association: key ":" value

$key: primitive

$values:
      value ("," value)*
    | EOL (value EOL)+
    | " "  ! No values.

$value: collection | primitive

$primitive: BOOLEAN | COMPLEX | FLOAT | INTEGER | NIL | RUNE | STRING | UNSIGNED

$context: "(" TYPE ")"

